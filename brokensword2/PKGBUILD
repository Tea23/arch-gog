# Maintainer: Joe Davison <joedavison.davison@gmail.com>

pkgname=gog-brokensword2
_gogname=broken_sword_2
pkgver=1.0.0.1
pkgrel=1
pkgdesc="Classic adventure game by Revolution Games, the sequel to Broken Sword"
arch=('i686' 'x86_64')
url="http://www.gog.com"
license=('custom:gog.com')
depends=('scummvm')
makedepends=('innoextract')
options=(!strip)
source=(${_gogname}
		${_gogname}.png
		${_gogname}.desktop)
md5sums=('976749b2578014e1f34aaf35ca4b41b9'
         '99a974582dd7f8a2a3da7f04ad5e73bb'
         '758a061879f4fcc048b5dd1f159ef00c')

# It takes a long time to compress and you should not distribute the built packages.
# This disables compression, speeding up the build process.
# You can remove this option or change it to .pkg.xz if you wish.
PKGEXT=.pkg.tar

build() {
	cd $srcdir

	if [ ! -f setup_${_gogname}.exe ]; then
		error "You must have setup_${_gogname}.exe present in the source dir: $PWD"
		error "Download the game from your GOG shelf and try again. Stopping."
		return 1
	else
		innoextract setup_${_gogname}.exe	
		# remove useless stuff
		rm -rf tmp/ sys/
		rm -rf "code:getdosboxfromini|path"
		rm -rf app/ScummVM
		
		# gog specific stuff (gogwrap etc, we don't need it)
		rm app/*.ico app/*.txt app/bsword2.ini
	fi
}

package() {
	mkdir -p $pkgdir/opt/gog/${_gogname}/
	mv $srcdir/app $pkgdir/opt/gog/${_gogname}/base

	install -D -m 755 $srcdir/${_gogname} \
		$pkgdir/usr/bin/${_gogname}

	install -D -m 644 $srcdir/${_gogname}.png \
		$pkgdir/usr/share/pixmaps/${_gogname}.png

	install -D -m 644 $srcdir/${_gogname}.desktop \
		$pkgdir/usr/share/applications/${_gogname}.desktop
}
